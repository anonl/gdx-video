buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:0.14+"
    }
}

ext {
    projectGroup = "gdx-video"

    gdxVersion = "1.9.3"
    slf4jVersion = '1.7.21'
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java'

    version = '0.0.2-anonl'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    
    plugins.withType(JavaPlugin) {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
        compileJava.options.encoding = 'UTF-8'
        compileTestJava.options.encoding = 'UTF-8'
    }    
}

project(":gdx-video") {

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
    }

}

project(":gdx-video-desktop") {

    configurations {
        excluded
        compile.extendsFrom excluded
    }

    dependencies {
        compile project(":gdx-video")
        excluded "com.badlogicgames.gdx:gdx-jnigen:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"

        runtime "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }

}

project(":gdx-video-android") {
    configurations { natives }

    dependencies {
        compile project(":gdx-video")
        compile files("libs/android-4.4.jar")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"

        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion"
    }

}

