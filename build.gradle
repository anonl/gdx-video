buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:0.14+"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

ext {
    projectGroup = "gdx-video"

    gdxVersion = "1.9.3"
    slf4jVersion = '1.7.21'
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'nl.weeaboo.gdx.video'
    version = '0.0.2-anonl'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        flatDir { dirs "$rootProject.projectDir/libs" }
    }
}

subprojects {
    apply plugin: 'java'

    plugins.withType(JavaPlugin) {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
        compileJava.options.encoding = 'UTF-8'
        compileTestJava.options.encoding = 'UTF-8'
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }
}

project(":gdx-video") {

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
    }

}

project(":gdx-video-desktop") {

    configurations {
        excluded
        compile.extendsFrom excluded
    }

    dependencies {
        compile project(":gdx-video")
        excluded "com.badlogicgames.gdx:gdx-jnigen:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"

        runtime "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        runtime "$group:gdx-video-desktop-natives:$version:"
    }

}

project(":gdx-video-android") {
    configurations { natives }

    dependencies {
        compile project(":gdx-video")
        compile files("libs/android-4.4.jar")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"

        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion"
    }

}

apply plugin: 'maven-publish'

publishing {
    publications {
        common(MavenPublication) {
            artifactId = 'gdx-video'

            def proj = project(":gdx-video")
            from proj.components.java            
            artifact(proj.sourceJar)            
        }
        android(MavenPublication) {
            artifactId = 'gdx-video-android'

            def proj = project(":gdx-video-android")
            from proj.components.java            
            artifact(proj.sourceJar)            
        }
        desktop(MavenPublication) {
            artifactId = 'gdx-video-desktop'

            def proj = project(":gdx-video-desktop")
            from proj.components.java            
            artifact(proj.sourceJar)            
        }
        desktop_natives(MavenPublication) {
            artifactId = 'gdx-video-desktop-natives'

            artifact "libs/gdx-video-desktop-natives-${version}.jar"
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['common', 'android', 'desktop', 'desktop_natives']
    pkg {
        repo = 'gdx-video'
        name = 'gdx-video'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/anonl/gdx-video.git'
        version {
            name = project.version
        }
    }
}
